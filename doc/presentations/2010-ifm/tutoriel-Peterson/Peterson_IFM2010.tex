\documentclass{enonce}
\selectlanguage{english}
\usepackage{palatino,mathpazo,color}
\usepackage{tutorial}
\setlength{\overfullrule}{3pt}
\course{IFM 2010 - TLAPS tutorial}
\title{Peterson algorithm}
\author{D. Cousineau, S. Merz}
%\institute{INRIA Lorraine - MSR INRIA Joint Lab}
\date{}

\begin{document}
\maketitle

\noindent In this tutorial, we shall prove that Peterson's algorithm satisfies the mutual exclusion property, using the TLA Proof System and the Toolbox IDE.

%\tableofcontents

\section{Toolbox and TLAPS install}
If you have not done it yet, you can download the Toolbox from 
\begin{center}
\url{http://www.tlaplus.net/tools/tla-toolbox/}
\end{center}
and dowload the TLA Proof System from
\begin{center}
\url{http://www.msr-inria.inria.fr/~doligez/tlaps/}
\end{center}

\noindent Binaries are available for Windows, MacOS and Linux distributions. Notice that Cygwin (version 1.7 or higher) is required to install TLAPS on Windows.

\section{Peterson Algorithm}
This algorithm, formulated by Gary L. Peterson in 1981, is a concurrent programming algorithm for mutual exclusion that allows two processes to share a single-use resource without conflict, using only shared memory for communication.

\subsection{Description}
The algorithm allows two processes to share some resource with the three following properties:
\begin{itemize}
\item Mutual exclusion : the two processes cannot access the resource at the same time
\item Progress: a process cannot immediately re-access the resource if the other process wants also to access the resource.
\item Bounded waiting: there exists a limit on the number of times that the other process is allowed to access the resource after a process has made a request to access the resource.
\end{itemize}

\noindent We shall focus on the mutual exclusion property in the TLA+ specification we are to build.

\medskip

\noindent We call \textit{critical section} the moment when some process is accessing the shared resource. The Mutual Exclusion property ensures that the two processes are not both in the critical section at the same time.

\medskip

 The idea of the algorithm is the following: we use two variables, a function called {\tt flag}, which says for each of the processes, if it wants to access the resource or not. And a simple variable {\tt turn} which holds the process whose turn it is to enter the critical section.

\noindent At the beginning of the algorithm, {\tt flag[0]} and {\tt flag[1]} are equal to {\tt FALSE} (none of the processes wants to access the resource), and {\tt turn} is equal to {\tt 0} (process 0 has priority to access the resource).

\noindent Then, for making process {\tt 0} (for example) access the shared resource, we do the following:
\begin{enumerate}
	\item[({\tt a1})] turn the value of {\tt flag[0]} to {\tt TRUE} \\(process {\tt 0} does want to enter the critical section)
	\item[({\tt a2})] turn the value of {\tt turn} to {\tt 1} \\(give the priority to the other process)
	\item[({\tt a3})] wait for  {\tt flag[1]} to be equal to {\tt FALSE} and {\tt turn} to be equal to {\tt 1} \\(process {\tt 1} does not want to access the resource and process {\tt 0} has priority to access it)
	\item[({\tt cs})] enter the critical section \\(access the resource)
	\item[({\tt a4})] turn  the value of {\tt flag[0]} to {\tt FALSE} \\(process {\tt 0} does not want to access the resource anymore)
\end{enumerate}   

\medskip


\subsection{PlusCal version}

Peterson's algorithm can be expressed in the PlusCal language as follows:
\smallskip

\begin{verbatim}
Not(i) == IF i = 0 THEN 1 ELSE 0

--algorithm Peterson {
   variables flag = [i \in {0, 1} |-> FALSE], turn = 0;
   process (proc \in {0,1}) {
     a0: while (TRUE) {
     a1:   flag[self] := TRUE;
     a2:   turn := Not(self);
     a3a:  if (flag[Not(self)]) {goto a3b} else {goto cs} ;
     a3b:  if (turn = Not(self)) {goto a3a} else {goto cs} ;
     cs:   skip;  \* critical section
     a4:   flag[self] := FALSE;
     } \* end while
    } \* end process
  }
\end{verbatim}

\noindent Notice that, for simplicity, the TLA+ specification we describe in the next section differs a little bit from the one that can be directly translated from the algorithm above, by the PlusCal translator.

\section{The specification}
Peterson's algorithm for two processes can already be verified by the Toolbox model-checker. But we use it as an exercise to make you learn ho to use TLA Proof Sytem.

\medskip

\noindent Peterson's TLA+ specification can be downloaded at 
\begin{center}
\url{http://www.msr-inria.inria.fr/~doligez/tlaps/IFM2010/Peterson.tla}
\end{center}
Let us describe what you can find in this file.

\subsection{Variables}
We declare three variables, {\tt flag}, {\tt turn} and {\tt pc} (which gives the current step of the algorithm).
We define {\tt vars} as the tuple composed by those three variables, and {\tt ProcSet} as the set $\{0,1\}$.
\begin{verbatim}
VARIABLES flag, turn, pc
vars == << flag, turn, pc >>
ProcSet == ({0,1})
\end{verbatim}

\subsection{The {\tt Init} state}
We then express the initial state of the algorithm as follows:
\begin{verbatim}
Init == /\ flag = [i \in {0, 1} |-> FALSE]
        /\ turn = 0
        /\ pc = [self \in ProcSet |-> CASE self \in {0,1} -> "a0"]
\end{verbatim}
{\tt flag[0]} and {\tt flag[1]} are equal to {\tt FALSE}.
\\ {\tt turn} is equal to {\tt 0}.
\\ Both processes are entering the while spin.

\subsection{The {\tt Next} action}
In order to define the {\tt Next} action of Peterson's algorithm, we have to describe, for each process, every possible step of the algorithm. For example the {\tt a0} step describes the fact that the considered process ({\tt self}) is going from line {\tt a0} to line {\tt a1} in the algorithm (in that case, values of variables {\tt flag} and {\tt turn} don't change).

\begin{verbatim}
a0(self) == /\ pc[self] = "a0"
            /\ pc' = [pc EXCEPT ![self] = "a1"]
            /\ UNCHANGED << flag, turn >>
\end{verbatim}

\noindent The second line above expresses the fact that the next value of the function {\tt pc} is the same as the current one, except that its value on {\tt self} is now {\tt "a1"}.

\medskip

\noindent We then do the same for each possible step of the algorithm:
\begin{verbatim}
a1(self) == /\ pc[self] = "a1"
            /\ flag' = [flag EXCEPT ![self] = TRUE]
            /\ pc' = [pc EXCEPT ![self] = "a2"]
            /\ UNCHANGED turn

a2(self) == /\ pc[self] = "a2"
            /\ turn' = Not(self)
            /\ pc' = [pc EXCEPT ![self] = "a3a"]
            /\ UNCHANGED flag

a3a(self) == /\ pc[self] = "a3a"
             /\ IF flag[Not(self)]
                   THEN /\ pc' = [pc EXCEPT ![self] = "a3b"]
                   ELSE /\ pc' = [pc EXCEPT ![self] = "cs"]
             /\ UNCHANGED << flag, turn >>

\end{verbatim}
\newpage
\begin{verbatim} 
a3b(self) == /\ pc[self] = "a3b"
             /\ IF turn = Not(self)
                   THEN /\ pc' = [pc EXCEPT ![self] = "a3a"]
                   ELSE /\ pc' = [pc EXCEPT ![self] = "cs"]
             /\ UNCHANGED << flag, turn >>

cs(self) == /\ pc[self] = "cs"
            /\ TRUE
            /\ pc' = [pc EXCEPT ![self] = "a4"]
            /\ UNCHANGED << flag, turn >>

a4(self) == /\ pc[self] = "a4"
            /\ flag' = [flag EXCEPT ![self] = FALSE]
            /\ pc' = [pc EXCEPT ![self] = "a0"]
            /\ UNCHANGED turn
\end{verbatim}

\noindent Now we can define {\tt proc(self)} as the fact that one of the previous actions is being accomplished:
\begin{verbatim}
proc(self) == \/ a0(self) \/ a1(self) \/ a2(self) \/ a3a(self) 
              \/ a3b(self) \/ cs(self) \/ a4(self)
\end{verbatim}
          
\noindent Finally, we define the {\tt Next} action, as the fact that either {\tt proc} is accomplished for one of the processes, or the algorithm has finished (to prevent deadlock on termination).

\begin{verbatim}         
Next == \E self \in {0,1}: proc(self)
\end{verbatim}

\medskip
\subsection{Specification}
The specification of the algorithm is given by the facts that the initial state is satisfied and that at every step either the action {\tt Next} is satisfied or the variables in {\tt vars} keep their values.

\begin{verbatim}
Spec == Init /\ [][Next]_vars
\end{verbatim}

\medskip
\subsection{Mutual Exclusion}
%\noindent The termination property is defined by the fact that there exists some state such that for that state and all the next ones, both processes are in step {\tt "Done"}.
% 
%\begin{verbatim}
%Termination == <>(\A self \in ProcSet: pc[self] = "Done")
%\end{verbatim}
%
%\noindent The safeness property is defined by the fact that the two processes don't access the resource at the same time.
%
%\begin{verbatim}
%Safe == \E i \in {0, 1} : pc[i] # "cs"
%\end{verbatim}
%
%\noindent The liveness property, is defined by the fact that both processes access the resource at some time.
%
%\begin{verbatim}
%Live == \A i \in {0, 1} : []<>(pc[i] = "cs")
%\end{verbatim}

The property we want the algorithm to satisfy can be defined as the fact that {\tt pc[0]} and {\tt pc[1]} have not both value {\tt "cs"} at the same time.

\begin{verbatim}
MutualExclusion == ~ (pc[0] = "cs" /\ pc[1] = "cs")
\end{verbatim}

\medskip
\subsection{The invariant}
Let us first define the property that ensures well-typedness of the variables .
%\textcolor{red}{sm: if you remove the second disjunct of Next, also remove that remark.}

\begin{verbatim}
TypeOK == /\ pc \in [{0,1} -> {"a0","a1","a2","a3a","a3b","cs","a4"}]
          /\ turn \in {0,1}
          /\ flag \in [{0,1} -> BOOLEAN]
\end{verbatim}

\noindent Now we can define the invariant we want the algorithm to satisfy, in order to prove the \linebreak {\tt MutualExclusion} property. It is defined as the fact that for each process {\tt i}, 
\begin{itemize}
\item if it is in step {\tt "a2"}, {\tt "a3a"}, {\tt "a3b"}, {\tt "cs"} or {\tt "a4"}, then its flag is equal to {\tt TRUE} (i.e. it does want to access to the resource)
\item if it is in step {\tt "cs"} or {\tt "a4"}, then the other process is not in one of these steps, and if the other process is in step {\tt "a3a"} or {\tt "a3b"}  then process {\tt i} has priority to access the resource.
\end{itemize}

\smallskip
          
\begin{verbatim}          
I == \A i \in {0, 1} :
       /\ (pc[i] \in {"a2", "a3a", "a3b", "cs", "a4"} => flag[i])
       /\ (pc[i] \in {"cs", "a4"})
            => /\ pc[Not(i)] \notin {"cs", "a4"}
               /\ (pc[Not(i)] \in {"a3a", "a3b"}) => (turn = i)
\end{verbatim}

\noindent Finally, we can define the actual invariant as the conjunction of the previous one and the fact that {\tt TypeOK} is satisfied. 

\begin{verbatim}
FullInv == TypeOK /\ I
\end{verbatim}


\section{Checking a simple proof from the Toolbox}
\label{simple}
As usual when we want to prove that a property is always satisfied (here {\tt MutualExclusion}), we reason by induction and define a invariant property (here {\tt FullInv}) such that:
\begin{enumerate}
\item the initial state (here {\tt Init}) satisfies the invariant,
\item the invariant is preserved by the next-state relation (here {\tt [Next]\_vars}),
\item the invariant ensures the correctness property ({\tt MutualExclusion}).  
\end{enumerate}

\noindent Proving the first fact is obvious. Let us check that by asking TLAPS to prove the following theorem (recall that we have to indicate explicitly which definitions we want to be usable in a proof):

\begin{verbatim}
THEOREM InitFullInv == Init => FullInv
  BY DEF Init, FullInv, TypeOK, I, ProcSet
\end{verbatim}

%\textcolor{red}{sm: this should be \texttt{FullInvariant}, not \texttt{MutualExclusion}}

\noindent Now, in the Toolbox, put the cursor on the first line of that theorem and then right-click on "Prove step or module". The Toolbox then colors that theorem in green, which means that it is proved (in this case, Zenon has succeeded in finding a proof).

\section{The main theorem}

\noindent Let us now focus fact number 2 above. For proving that fact it is easier to an intermediate lemma, which states that property {\tt TypeOK} is preserved by the next-state relation.

%consider two intermediate lemmas. The first one expresses that if {\tt TypeOK} is satisfied, then any of the process can be in step {\tt "Done"}. It comes directly from the definitions of {\tt TypeOK} and {\tt ProcSet}.
%\begin{verbatim}
%LEMMA NeverDone == 
%          TypeOK /\ (\A self \in ProcSet: pc[self] = "Done") => FALSE
%     BY DEFS TypeOK, ProcSet
%\end{verbatim}
%
%\textcolor{red}{sm: this lemma would disappear, and the proof of the subsequent one simplified}
%\noindent The second one states that property {\tt TypeOK} is preserved by the next-state relation.

\begin{verbatim}
LEMMA TypeCorrect == TypeOK /\ Next => TypeOK'
  <1>1. ASSUME NEW i \in {0,1}
        PROVE  TypeOK /\ proc(i) => TypeOK'
    BY DEFS TypeOK, proc, a0, a1, a2, a3a, a3b, cs, a4, Not
  <1>. QED
    BY <1>1, NeverDone DEF Next
\end{verbatim}

\noindent When asking TLAPS to prove the theorem above, you can notice that step {\tt<1>1} gets first colored black. That means that Zenon has been trying to prove it for more than three seconds. Then it gets colored red. That means that Zenon has failed to find a proof with the default timeout (ten seconds). Finally, it gets colored green since Isabelle succeeds in proving it.

\bigskip

\noindent Finally, let us prove the second item of section \ref{simple}.

\begin{verbatim}
THEOREM Inductive == TypeOK /\ I /\ Next => I'
\end{verbatim}

\noindent We first rephrase the sequent to be proved in a more perspicuous form:

\begin{verbatim}
<1>1. ASSUME TypeOK,
             I,
             NEW i \in {0,1},
             proc(i)
      PROVE  I'
\end{verbatim}

\noindent We then assert {\tt TypeOK'} (that will be useful in the following of the proof):

\begin{verbatim}
  <2>0. TypeOK'
\end{verbatim}

\noindent We then split the result we want to prove into three assertions:

\begin{verbatim}
  <2>1. ASSUME NEW j \in {0,1},
               pc'[j] \in {"a2", "a3a", "a3b", "cs", "a4"}
        PROVE  flag'[j]
        
  <2>2. ASSUME NEW j \in {0,1},
               pc'[j] \in {"cs", "a4"}
        PROVE pc'[Not(j)] \notin {"cs", "a4"}
        
  <2>3. ASSUME NEW j \in {0,1},
               pc'[j] \in {"cs", "a4"},
               pc'[Not(j)] \in {"a3a", "a3b"}
        PROVE turn' = j
\end{verbatim}

\noindent Those three assertions can be proved by case on the step in which process {\tt i} is.
If you ask TLAPS to prove that theorem, you can notice that all those proof-steps are colored yellow. That means that the proof is missing. It's up to you now to make them green...

\end{document}


%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%


\begin{appendix}
\section{Prover options}







\section{Practical hints}

The TLA+ proof system is designed to check the validity of claims as
independently as possible of specific proof back-ends. We believe that users
should concentrate on writing proofs in terms of their particular applications,
not in terms of the capabilities of a particular proof system. In particular,
TLAPS invokes its back-ends with some default setup for automatic proof, and we
try to make it hard for users to change this default setup. Expert users of
back-end provers may be frustrated because they may have to develop proofs
somewhat further than what would be necessary with a fine-tuned tactic script.
The main payoff of limited access to the nitty-gritty details of provers is
greater clarity of the resulting proofs. They are also easier to maintain across
minor changes of the specification or new releases of the TLA prover.

On some occasions users will encounter situations where the prover cannot prove
an "obvious" proof obligation. Here are a few hints on what to try to make the
proof go through. Your additions to this list are welcome.


\subsection{Control the size of formulas and expressions}
Our provers are currently not good at making abstractions that humans understand
immediately. They are easily confused by moderately big proof obligations and
are just as likely to work on a top-level conjunction as on a set construction
buried deeply inside the formula. This can cause back-ends to become very slow
or even unable to complete seemingly trivial steps. While we intend to improve
the back-ends in this respect, you can help them by using local definitions in
proofs and hiding these definitions in order to keep expressions small. (Keep in
mind that definitions introduced in a proof are usable by default and must be
hidden explicitly, unlike definitions in specifications, which must be
explicitly $\USE$d.)

\medskip

\noindent Here is a contrived example:

\begin{verbatim}
LEMMA  /\ x \in SomeVeryBigExpression
       /\ y \in AnotherBigExpression
   <=>
       /\ y \in AnotherBigExpression
       /\ x \in SomeVeryBigExpression
       
<1> DEFINE S == SomeVeryBigExpression
    \** here and in the following, you may use positional names to
    \** avoid repeating the big expressions
<1> DEFINE T == AnotherBigExpression
<1>1. x \in S <=> x \in SomeVeryBigExpression
  OBVIOUS
    \** The provers should not have any trouble proving that
    \** a very large expression is equivalent to itself.  If it
    \** can't prove this, you've almost certainly made a mistake
    \** in the definition of S.
<1>2. y \in T <=> y \in AnotherBigExpression
  OBVIOUS
<1> HIDE DEF S, T
<1>3. /\ x \in S
      /\ y \in T
  <=> 
      /\ y \in T
      /\ x \in S
  OBVIOUS
<1>4. QED
  BY <1>1, <1>2, <1>3
\end{verbatim}

\medskip

This kind of problem typically arises when reasoning about LET expressions,
which are \linebreak silently expanded by the proof manager. In a proof, introduce local
definitions corresponding to the LET (using copy and paste from the
specification or subexpression names), show that the overall expression equals the body of the LET
(trivial by reflexivity), establish the necessary facts about these locally
defined operators, and HIDE the definitions afterwards.

\medskip

Introducing definitions to hide irrelevant expressions is a useful
thing to do whenever you're having trouble proving something.  It
makes the obligations whose proofs fail easier to read, making
mistakes easier to find.

\subsection{Avoid "circular" (sets of) equations}

Rewriting is one effective way to reason about equations, and it underlies the
automatic proof methods used by the Isabelle back-end. The basic idea is to
orient equalities such that the expressions on the left-hand side are
systematically replaced by the right-hand sides. However, if the set of
equations contains cycles as in

\begin{verbatim}
s = f(t)
t = g(s)
\end{verbatim}

then rewriting may never terminate. Isabelle employs some (incomplete)
heuristics to detect such cycles and will refuse to rewrite equations that it
determines to be circular. This usually leads to its inability to infer anything
about these equations. If circularity is not detected, it may cause Isabelle to
enter an infinite loop. The suggested remedy is again to introduce local
definitions that are hidden to break the loops.

\medskip

\noindent As a concrete example consider the following proof snippet:

\begin{verbatim}
   <4>17. foo.name = "xyz"
     <5>1. foo = [name |-> "xyz", value = foo.value]
       BY <2>2
     <5>2. QED
       BY <5>1  \** may not work because <5>1 is a circular equation
\end{verbatim}

\noindent One possible workaround is as follows:

\begin{verbatim}
   <4>17. foo.name = "xyz"
     <5>   DEFINE fooval == foo.value
     <5>1. foo = [name |-> "xyz", value = fooval]
       BY <2>2
     <5>   HIDE DEF fooval
     <5>2. QED
       BY <5>1
\end{verbatim}
%\end{appendix}
%\end{document}
\subsection{Reasoning about CHOOSE expressions}

Consider a definition such as

\begin{verbatim}
  foo == CHOOSE x \in S : P(x)
\end{verbatim}

In order to prove a property Q(foo), you will typically prove the two following
assertions:

\begin{verbatim}
(a) \E x \in S : P(x)
(b) \A x \in S : P(x) => Q(x)
\end{verbatim}

In some cases, assertion (b) can be trivial and need not be shown explicitly.
Reasoning about an unbounded CHOOSE expression is analogous.

\medskip

Remember that CHOOSE always denotes some value, even if P(x) holds for no
$x \in S$ (in particular, if $S = \{\}$), in which case the CHOOSE expression is
fixed, but arbitrary. In practice, CHOOSE expressions usually arise when
condition (a) is satisfied. Should you have designed your property to work even
if the domain of the CHOOSE is empty, property Q must be trivial in that case,
and you can structure your proof as follows:

\begin{verbatim}
  <3>5. Q(foo)
    <4>1. CASE \E x \in S : P(x)
      <5>1. \A x \in S : P(x) => Q(x)
      <5>2. QED
        BY <4>1, <5>1
    <4>2. CASE ~ \E x \in S : P(x)
      <5>1. \A x : Q(x)
      <5>2. QED
        BY <5>1
    <4>3. QED
      BY <4>1, <4>2
\end{verbatim}

A frequent TLA+ idiom is to define a "null" value by writing

\begin{verbatim}
NoValue == CHOOSE x : x \notin Value
\end{verbatim}

The laws of set theory ensure that no set is universal, hence there exists an x
that is not an element of set Value, ensuring condition (a) above. The theorem
NoSetContainsEverything in the standard module TLAPS can be used to prove this
condition.

\subsection{Help the Prover When Reasoning About Records}

In one proof, we had

\begin{verbatim}
    mb == [type  |-> "1b", bal |-> b, acc |-> self,
          mCBal |-> maxCBal[self], mCVal |-> maxCVal[self]]
\end{verbatim}

and were trying to prove

\begin{verbatim}
    m1 # mb /\ m2 # mb
\end{verbatim}

from facts that included

\begin{verbatim}
    m1.type = "2av" /\ m2.type = "2av"
\end{verbatim}

Zenon failed on the proof and Isabelle proved it only after a long
time.  (In fact, we originally stopped the proof because it was taking
so long.)  However, Zenon proved it instantly when we added $mb.type =
"1b"$ to the BY statement's list of facts.  The provers are reluctant
to try finding relations of the form record.field = value; they often
need help.


\subsection{Divide and Conquer}

When the provers can't prove something that you think is obvious, it's
usually because it isn't true.  You can easily spend hours looking at
a proof obligation without noticing a tiny mistake.  The best way to
find a mistake is by breaking the proof into simpler steps.
Continuing to do this on the step or steps whose proof fails will
eventually lead you to discover the problem--usually a missing
hypothesis or a mistake in a formula.  When you correct the mistake in
the original proof step, the prover will usually be able to prove it.

\subsection{Don't Reinvent Mathematics}

We expect that most people who use TLAPS will do so because they want
to verify properties of an algorithm or system.  We have therefore not
devoted our limited resources to building libraries of mathematical
results.  If you want to create such libraries, we would welcome your
help.  However, if you are concerned with an algorithm or system, you
should not be spending your time proving basic mathematical facts.
Instead, you should assert the mathematical theorems you need as
assumptions or theorems.

\medskip

Asserting facts is dangerous, because it's easy to make a mistake and
assert something false, making your entire proof unsound.
Fortunately, you can use the TLC model checker to avoid such mistakes.
For example, our example correctness proof of Euclid's algorithm uses
this assumption

\begin{verbatim}
    ASSUME GCDProperty3 == 
           \A p, q \in Nat \ {0}: (p < q) => GCD(p, q) = GCD(p, q-p)
\end{verbatim}

TLC cannot check this assumption because it can't evaluate a
quantification over an infinite set.  However, you can tell TLC to
replace the definition of Nat with

\begin{verbatim}
    Nat == 0..50
\end{verbatim}

(In the Toolbox, use the Definition Override section of the model's
Advanced Options page.)  TLC quickly verifies this assumption.  (TLC
checks each ASSUME; to add an assumption that you don't want TLC to check,
make it an AXIOM.)  

This kind of checking is almost certain to catch an error in
expressing a fundamentally correct mathematical result--except when
the only counterexamples are infinite.  Fortunately, this is rarely
the case when the result is needed for reasoning about an algorithm or
system.

\subsection{It's Easier to Prove Something if it's True}

Before trying to prove a property of an algorithm or system, try to
check it with TLC. Even if TLC cannot check a large enough model to
catch all errors, running it on a small model can still catch many
simple errors.  You will save a lot of time if you let TLC find these
errors instead of discovering them while writing the proof.


\end{appendix}

\end{document}
